// Copyright (c) 2023 Cyber (See LICENSE)

/// Fibers.

const std = @import("std");
const builtin = @import("builtin");
const build_options = @import("build_options");
const stdx = @import("stdx");
const t = stdx.testing;
const cy = @import("cyber.zig");
const vmc = @import("vm_c.zig");
const rt = cy.rt;
const log = cy.log.scoped(.fiber);
const Value = cy.Value;
const bt = cy.types.BuiltinTypes;

pub const PanicPayload = u64;

pub const PanicType = enum(u8) {
    uncaughtError = vmc.PANIC_UNCAUGHT_ERROR,
    staticMsg = vmc.PANIC_STATIC_MSG,
    msg = vmc.PANIC_MSG,
    nativeThrow = vmc.PANIC_NATIVE_THROW,
    inflightOom = vmc.PANIC_INFLIGHT_OOM,
    none = vmc.PANIC_NONE,
};

test "fiber internals." {
    if (cy.is32Bit) {
        try t.eq(@sizeOf(vmc.Fiber), 72);
        try t.eq(@sizeOf(vmc.TryFrame), 12);
    } else {
        try t.eq(@sizeOf(vmc.Fiber), 88);
        try t.eq(@sizeOf(vmc.TryFrame), 12);
    }
}

pub fn allocFiber(vm: *cy.VM, pc: usize, args: []const cy.Value, argDst: u8, initialStackSize: u32) !cy.Value {
    // Args are copied over to the new stack.
    var stack = try vm.alloc.alloc(Value, initialStackSize);
    // Assumes initial stack size generated by compiler is enough to hold captured args.
    @memcpy(stack[argDst..argDst+args.len], args);

    const obj: *vmc.Fiber = @ptrCast(try cy.heap.allocExternalObject(vm, @sizeOf(vmc.Fiber), true));
    const parentDstLocal = cy.NullU8;
    obj.* = .{
        .typeId = bt.Fiber | vmc.CYC_TYPE_MASK,
        .rc = 1,
        .stackPtr = @ptrCast(stack.ptr),
        .stackLen = @intCast(stack.len),
        .pcOffset = @intCast(pc),
        .argStart = argDst,
        .numArgs = @intCast(args.len),
        .stackOffset = 0,
        .parentDstLocal = parentDstLocal,
        .tryStackCap = 0,
        .tryStackPtr = undefined,
        .tryStackLen = 0,
        .throwTracePtr = undefined,
        .throwTraceCap = 0,
        .throwTraceLen = 0,
        .initialPcOffset = @intCast(pc),
        .panicPayload = undefined,
        .panicType = vmc.PANIC_NONE,
        .prevFiber = undefined,
    };

    return Value.initCycPtr(obj);
}

/// Since this is called from a coresume expression, the fiber should already be retained.
pub fn pushFiber(vm: *cy.VM, curFiberEndPc: usize, curFramePtr: [*]Value, fiber: *cy.Fiber, parentDstLocal: u8) PcFp {
    // Save current fiber.
    vm.c.curFiber.stackPtr = @ptrCast(vm.c.stack);
    vm.c.curFiber.stackLen = @intCast(vm.c.stack_len);
    vm.c.curFiber.pcOffset = @intCast(curFiberEndPc);
    vm.c.curFiber.stackOffset = @intCast(getStackOffset(vm.c.stack, curFramePtr));

    // Push new fiber.
    fiber.prevFiber = vm.c.curFiber;
    fiber.parentDstLocal = parentDstLocal;
    vm.c.curFiber = fiber;
    vm.c.stack = @ptrCast(fiber.stackPtr);
    vm.c.stack_len = fiber.stackLen;
    vm.c.stackEndPtr = vm.c.stack + fiber.stackLen;
    // Check if fiber was previously yielded.
    if (vm.c.ops[fiber.pcOffset].opcode() == .coyield) {
        log.tracev("fiber set to {} {*}", .{fiber.pcOffset + 3, vm.c.framePtr});
        return .{
            .pc = toVmPc(vm, fiber.pcOffset + 3),
            .fp = @ptrCast(fiber.stackPtr + fiber.stackOffset),
        };
    } else {
        log.tracev("fiber set to {} {*}", .{fiber.pcOffset, vm.c.framePtr});
        return .{
            .pc = toVmPc(vm, fiber.pcOffset),
            .fp = @ptrCast(fiber.stackPtr + fiber.stackOffset),
        };
    }
}

pub fn popFiber(vm: *cy.VM, cur: PcFpOff, retValue: Value) PcFpOff {
    vm.c.curFiber.stackPtr = @ptrCast(vm.c.stack);
    vm.c.curFiber.stackLen = @intCast(vm.c.stack_len);
    vm.c.curFiber.pcOffset = cur.pc;
    vm.c.curFiber.stackOffset = cur.fp;
    const dstLocal = vm.c.curFiber.parentDstLocal;

    // Release current fiber.
    const nextFiber = vm.c.curFiber.prevFiber.?;
    cy.arc.releaseObject(vm, cy.ptrAlignCast(*cy.HeapObject, vm.c.curFiber));

    // Set to next fiber.
    vm.c.curFiber = nextFiber;

    // Copy return value to parent local.
    if (dstLocal != cy.NullU8) {
        vm.c.curFiber.stackPtr[vm.c.curFiber.stackOffset + dstLocal] = @bitCast(retValue);
    } else {
        cy.arc.release(vm, retValue);
    }

    vm.c.stack = @ptrCast(vm.c.curFiber.stackPtr);
    vm.c.stack_len = vm.c.curFiber.stackLen;
    vm.c.stackEndPtr = vm.c.stack + vm.c.curFiber.stackLen;
    log.tracev("fiber set to {} {*}", .{vm.c.curFiber.pcOffset, vm.c.framePtr});
    return PcFpOff{
        .pc = vm.c.curFiber.pcOffset,
        .fp = vm.c.curFiber.stackOffset,
    };
}

/// Unwinds the stack and releases the locals.
/// This also releases the initial captured vars since it's on the stack.
pub fn releaseFiberStack(vm: *cy.VM, fiber: *cy.Fiber) !void {
    log.tracev("release fiber stack, start", .{});
    defer log.tracev("release fiber stack, end", .{});
    var stack = @as([*]Value, @ptrCast(fiber.stackPtr))[0..fiber.stackLen];
    var framePtr = fiber.stackOffset;
    var pc = fiber.pcOffset;

    if (pc != cy.NullId) {
        // Check if fiber was previously on a yield op.
        if (vm.c.ops[pc].opcode() == .coyield) {
            // The yield statement contains the alive locals.
            const localsStart = vm.c.ops[pc+1].val;
            const localsEnd = vm.c.ops[pc+2].val;
            log.tracev("release on frame {} {} localsEnd: {}", .{framePtr, pc, localsEnd});
            for (stack[framePtr+localsStart..framePtr+localsEnd]) |val| {
                cy.arc.release(vm, val);
            }

            // Prev frame.
            pc = @intCast(getInstOffset(vm.c.ops, stack[framePtr + 2].retPcPtr) - stack[framePtr + 1].retInfoCallInstOffset());
            framePtr = @intCast(getStackOffset(stack.ptr, stack[framePtr + 3].retFramePtr));

            // Unwind stack and release all locals.
            while (framePtr > 0) {
                const symIdx = try cy.debug.indexOfDebugSym(vm, pc);
                const sym = cy.debug.getDebugSymByIndex(vm, symIdx);
                const tempIdx = cy.debug.getDebugTempIndex(vm, symIdx);

                const locals = sym.getLocals();
                log.tracev("release on frame {} {}, locals: {}-{}", .{framePtr, pc, locals.start, locals.end});

                if (tempIdx != cy.NullId) {
                    cy.arc.runTempReleaseOps(vm, stack.ptr + framePtr, tempIdx);
                }
                if (locals.len() > 0) {
                    cy.arc.releaseLocals(vm, vm.c.getStack(), framePtr, locals);
                }

                // Prev frame.
                pc = @intCast(getInstOffset(vm.c.ops, stack[framePtr + 2].retPcPtr) - stack[framePtr + 1].retInfoCallInstOffset());
                framePtr = @intCast(getStackOffset(stack.ptr, stack[framePtr + 3].retFramePtr));
            }
        }

        // Cleanup on main fiber block.
        if (vm.c.ops[pc].opcode() != .coreturn) {
            const symIdx = try cy.debug.indexOfDebugSym(vm, pc);
            const tempIdx = cy.debug.getDebugTempIndex(vm, symIdx);
            if (tempIdx != cy.NullId) {
                cy.arc.runTempReleaseOps(vm, stack.ptr + framePtr, tempIdx);
            }

            // No end locals for main fiber block yet.
            // const endLocalsPc = cy.debug.debugSymToEndLocalsPc(vm, sym);
            // if (endLocalsPc != cy.NullId) {
            //     cy.arc.runBlockEndReleaseOps(vm, stack, framePtr, endLocalsPc);
            // }
        }
    }

    // Release any binded args.
    if (fiber.numArgs > 0) {
        for (stack[fiber.argStart..fiber.argStart+fiber.numArgs]) |arg| {
            log.tracev("release fiber arg", .{});
            cy.arc.release(vm, arg);
        }
    }

    // Finally free stack.
    vm.alloc.free(stack);
}

// Determine whether it's a vm or host frame.
pub fn isVmFrame(_: *cy.VM, stack: []const Value, fpOff: u32) bool {
    return stack[fpOff+1].retInfoCallInstOffset() > 0;
}

/// Unwind from `ctx` and release each frame.
/// TODO: See if releaseFiberStack can resuse the same code.
pub fn unwindStack(vm: *cy.VM, stack: []const Value, ctx: PcFpOff) !PcFpOff {
    log.tracev("panic unwind {*}", .{stack.ptr + ctx.fp});
    var pc = ctx.pc;
    var fp = ctx.fp;

    vm.compactTrace.clearRetainingCapacity();

    while (true) {
        log.tracev("frame pc={} fp={}", .{pc, fp});
        if (fp == 0 or isVmFrame(vm, stack, fp)) {
            try vm.compactTrace.append(vm.alloc, .{
                .pcOffset = pc,
                .fpOffset = fp,
            });
            try releaseFrame(vm, fp, pc);
            if (fp == 0) {
                // Done, at main block.
                return PcFpOff{ .pc = pc, .fp = fp };
            } else {
                const prev = getVmFramePrev(vm, stack, fp);
                pc = prev.pc;
                fp = prev.fp;
            }
        } else {
            try vm.compactTrace.append(vm.alloc, .{
                .pcOffset = cy.NullId,
                .fpOffset = fp,
            });
            // Skip releasing host frame. Record it.
            const prev = getHostFramePrev(vm, stack, fp);
            pc = prev.pc;
            fp = prev.fp;
        }
    }
}

/// Walks the stack and records each frame.
pub fn recordCurFrames(vm: *cy.VM) !void {
    @setCold(true);
    log.tracev("recordCompactFrames", .{});

    var fp = cy.fiber.getStackOffset(vm.c.stack, vm.c.framePtr);
    var pc = cy.fiber.getInstOffset(vm.c.ops, vm.c.pc);
    while (true) {
        log.tracev("pc: {}, fp: {}", .{pc, fp});

        if (fp == 0 or cy.fiber.isVmFrame(vm, vm.c.getStack(), fp)) {
            try vm.compactTrace.append(vm.alloc, .{
                .pcOffset = pc,
                .fpOffset = fp,
            });
            if (fp == 0) {
                // Main.
                break;
            } else {
                const prev = getVmFramePrev(vm, vm.c.getStack(), fp);
                fp = prev.fp;
                pc = prev.pc;
            }
        } else {
            try vm.compactTrace.append(vm.alloc, .{
                .pcOffset = cy.NullId,
                .fpOffset = fp,
            });
            const prev = getHostFramePrev(vm, vm.c.getStack(), fp);
            fp = prev.fp;
            pc = prev.pc;
        }
    }
}

fn releaseFrame(vm: *cy.VM, fp: u32, pc: u32) !void {
    const symIdx = try cy.debug.indexOfDebugSym(vm, pc);
    const sym = cy.debug.getDebugSymByIndex(vm, symIdx);
    const tempIdx = cy.debug.getDebugTempIndex(vm, symIdx);
    const locals = sym.getLocals();
    log.tracev("release frame: pc={} {}, fp={} tempIdx: {}, locals: {}-{}", .{pc, vm.c.ops[pc].opcode(), fp, tempIdx, locals.start, locals.end});

    // Release temps.
    if (tempIdx != cy.NullId) {
        cy.arc.runTempReleaseOps(vm, vm.c.stack + fp, tempIdx);
    }

    // Release locals.
    if (locals.len() > 0) {
        cy.arc.releaseLocals(vm, vm.c.getStack(), fp, locals);
    }
}

fn releaseFrameTemps(vm: *cy.VM, fp: u32, pc: u32) !void {
    const symIdx = try cy.debug.indexOfDebugSym(vm, pc);
    const tempIdx = cy.debug.getDebugTempIndex(vm, symIdx);
    log.tracev("release frame temps: {} {}, tempIdx: {}", .{pc, vm.c.ops[pc].opcode(), tempIdx});

    // Release temps.
    cy.arc.runTempReleaseOps(vm, vm.c.stack + fp, tempIdx);
}

fn getHostFramePrev(vm: *cy.VM, stack: []const Value, fp: u32) PcFpOff {
    const prev_fp = getStackOffset(stack.ptr, stack[fp + 3].retFramePtr);
    const prev_pc = getInstOffset(vm.c.ops, stack[fp + 2].retPcPtr);
    return PcFpOff{ .pc = prev_pc, .fp = prev_fp };
}

fn getVmFramePrev(vm: *cy.VM, stack: []const Value, fp: u32) PcFpOff {
    const prevPc = getInstOffset(vm.c.ops, stack[fp + 2].retPcPtr) - stack[fp + 1].retInfoCallInstOffset();
    const prevFp = getStackOffset(stack.ptr, stack[fp + 3].retFramePtr);
    return PcFpOff{ .pc = prevPc, .fp = prevFp };
}

// Returns a continuation if there is a parent fiber otherwise null.
pub fn fiberEnd(vm: *cy.VM, ctx: PcFpOff) ?PcFpOff {
    if (vm.c.curFiber != &vm.c.mainFiber) {
        if (cy.Trace) {
            // Print fiber panic.
            cy.debug.printLastUserPanicError(vm) catch cy.fatal();
        }
        return cy.fiber.popFiber(vm, ctx, Value.initInt(0));
    } else return null;
}

/// Throws an error value by unwinding until the either the first matching catch block
/// is reached or `endFp` is reached.
/// If the main rootFp is reached without a catch block, the error is elevated to an uncaught panic error.
/// Records frames in `compactTrace`.
pub fn throw(vm: *cy.VM, endFp: u32, ctx: PcFpOff, err: Value) !PcFpOff {
    log.tracev("throw", .{});
    var tframe: vmc.TryFrame = undefined;
    var hasTryFrame = false;

    if (vm.c.tryStack_len > 0) {
        tframe = vm.c.tryStack[vm.c.tryStack_len-1];
        if (tframe.fp >= endFp) {
            // Only consider a tframe if it exists at or after `endFp`.
            vm.c.tryStack_len -= 1;
            hasTryFrame = true;
        }
    }

    vm.compactTrace.clearRetainingCapacity();
    var fp = ctx.fp;
    var pc = ctx.pc;

    while (true) {
        try vm.compactTrace.append(vm.alloc, .{
            .pcOffset = pc,
            .fpOffset = fp,
        });
        if (hasTryFrame) {
            if (fp > tframe.fp) {
                // Haven't reached target try block. Unwind frame.
                try releaseFrame(vm, fp, pc);
                const prev = getVmFramePrev(vm, vm.c.getStack(), fp);
                fp = prev.fp;
                pc = prev.pc;
            } else {
                if (cy.Trace and fp != tframe.fp) {
                    log.tracev("{} {}", .{fp, tframe.fp});
                    return error.Unexpected;
                }

                // Reached target try block.
                try releaseFrameTemps(vm, fp, pc);

                // Copy error to catch dst.
                if (tframe.catchErrDst != cy.NullU8) {
                    if (tframe.releaseDst) {
                        cy.arc.release(vm, vm.c.stack[fp + tframe.catchErrDst]);
                    }
                    vm.c.stack[tframe.fp + tframe.catchErrDst] = @bitCast(err);
                }
                // Goto catch block in the current frame.
                return PcFpOff{
                    .pc = tframe.catchPc,
                    .fp = fp,
                };
            }
        } else {
            // Unwind frame.
            try releaseFrame(vm, fp, pc);
            if (fp > endFp) {
                const prev = getVmFramePrev(vm, vm.c.getStack(), fp);
                fp = prev.fp;
                pc = prev.pc;
            } else {
                if (cy.Trace and fp != endFp) {
                    log.tracev("{} {}", .{fp, endFp});
                    return error.Unexpected;
                }
                
                // Reached end.

                if (fp == 0) {
                    // Main root. Convert to uncaught panic.
                    vm.c.curFiber.panicType = vmc.PANIC_UNCAUGHT_ERROR;

                    // Build stack trace.
                    const frames = try cy.debug.allocStackTrace(vm, vm.c.getStack(), vm.compactTrace.items());
                    vm.stackTrace.deinit(vm.alloc);
                    vm.stackTrace.frames = frames;

                    return fiberEnd(vm, .{ .pc = pc, .fp = fp }) orelse {
                        return error.Panic;
                    };
                } else {
                    // Gives host dispatch a chance to catch the error.
                    vm.c.curFiber.panicType = vmc.PANIC_UNCAUGHT_ERROR;
                    return error.Panic;
                }
            }
        }
    }
}

pub fn freeFiberPanic(vm: *const cy.VM, fiber: *vmc.Fiber) void {
    const panicT: PanicType = @enumFromInt(fiber.panicType);
    if (panicT == .msg) {
        const ptr: usize = @intCast(fiber.panicPayload & ((1 << 48) - 1));
        const len: usize = @intCast(fiber.panicPayload >> 48);
        vm.alloc.free(@as([*]const u8, @ptrFromInt(ptr))[0..len]);
    }
}

pub inline fn getInstOffset(from: [*]const cy.Inst, to: [*]const cy.Inst) u32 {
    return @intCast(@intFromPtr(to) - @intFromPtr(from));
}

pub inline fn getStackOffset(from: [*]const Value, to: [*]const Value) u32 {
    // Divide by eight.
    return @intCast((@intFromPtr(to) - @intFromPtr(from)) >> 3);
}

pub inline fn stackEnsureUnusedCapacity(self: *cy.VM, unused: u32) !void {
    if (@intFromPtr(self.framePtr) + 8 * unused >= @intFromPtr(self.stack.ptr + self.stack.len)) {
        try self.stackGrowTotalCapacity((@intFromPtr(self.framePtr) + 8 * unused) / 8);
    }
}

pub inline fn stackEnsureTotalCapacity(self: *cy.VM, newCap: usize) !void {
    if (newCap > self.c.stack_len) {
        try stackGrowTotalCapacity(self, newCap);
    }
}

pub fn stackEnsureTotalCapacityPrecise(self: *cy.VM, newCap: usize) !void {
    if (newCap > self.c.stack_len) {
        try stackGrowTotalCapacityPrecise(self, newCap);
    }
}

pub fn stackGrowTotalCapacity(self: *cy.VM, newCap: usize) !void {
    var betterCap = self.c.stack_len;
    while (true) {
        betterCap +|= betterCap / 2 + 8;
        if (betterCap >= newCap) {
            break;
        }
    }
    try stackGrowTotalCapacityPrecise(self, betterCap);
}

pub fn stackGrowTotalCapacityPrecise(self: *cy.VM, newCap: usize) !void {
    if (self.alloc.resize(self.c.getStack(), newCap)) {
        self.c.stack_len = newCap;
        self.c.stackEndPtr = self.c.stack + newCap;
    } else {
        const new_stack = try self.alloc.realloc(self.c.getStack(), newCap);
        self.c.stack = new_stack.ptr;
        self.c.stack_len = new_stack.len;
        self.c.stackEndPtr = self.c.stack + newCap;

        if (builtin.is_test or cy.Trace) {
            // Fill the stack with null heap objects to surface undefined access better.
            @memset(self.c.getStack(), Value.initCycPtr(&DummyHeapObject));
        }
    }
}

var DummyHeapObject = cy.HeapObject{
    .head = .{
        .typeId = cy.NullId,
        .rc = 0,
    },
};

pub inline fn toVmPc(self: *const cy.VM, offset: usize) [*]cy.Inst {
    return self.c.ops + offset;
}

// Performs stackGrowTotalCapacityPrecise in addition to patching the frame pointers.
pub fn growStackAuto(vm: *cy.VM) !void {
    @setCold(true);
    // Grow by 50% with minimum of 16.
    var growSize = vm.c.stack_len / 2;
    if (growSize < 16) {
        growSize = 16;
    }
    try growStackPrecise(vm, vm.c.stack_len + growSize);
}

pub fn ensureTotalStackCapacity(vm: *cy.VM, newCap: usize) !void {
    if (newCap > vm.c.stack_len) {
        var betterCap = vm.c.stack_len;
        while (true) {
            betterCap +|= betterCap / 2 + 8;
            if (betterCap >= newCap) {
                break;
            }
        }
        try growStackPrecise(vm, betterCap);
    }
}

fn growStackPrecise(vm: *cy.VM, newCap: usize) !void {
    if (vm.alloc.resize(vm.c.getStack(), newCap)) {
        vm.c.stack_len = newCap;
        vm.c.stackEndPtr = vm.c.stack + newCap;
    } else {
        const newStack = try vm.alloc.alloc(Value, newCap);

        // Copy to new stack.
        @memcpy(newStack[0..vm.c.stack_len], vm.c.stack);

        // Patch frame ptrs. 
        var curFpOffset = getStackOffset(vm.c.stack, vm.c.framePtr);
        while (curFpOffset != 0) {
            const prevFpOffset = getStackOffset(vm.c.stack, newStack[curFpOffset + 3].retFramePtr);
            newStack[curFpOffset + 3].retFramePtr = newStack.ptr + prevFpOffset;
            curFpOffset = prevFpOffset;
        }

        // Free old stack.
        vm.alloc.free(vm.c.getStack());

        // Update to new frame ptr.
        vm.c.framePtr = newStack.ptr + getStackOffset(vm.c.stack, vm.c.framePtr);
        vm.c.stack = newStack.ptr;
        vm.c.stack_len = newStack.len;
        vm.c.stackEndPtr = vm.c.stack + newCap;
    }
}

pub const PcFpOff = struct {
    pc: u32,
    fp: u32,
};

pub const PcFp = struct {
    pc: [*]cy.Inst,
    fp: [*]Value,
};